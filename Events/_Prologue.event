EventPointerTable(7, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
	TurnEventPlayer(0x0,TurnOneEvent,1)
	TurnEventPlayer(0xA,GalleyReinforcements,0,MaxTurns(0))
	TurnEventPlayer(0x0,Reinforcements,3)
	TurnEventPlayer(0x0,BrigandsAppear,6)
	END_MAIN

TurnOneEvent:
	CAM1 Sanders
	MUSC KnightErrandSong
	FlashCursor(Scott,60)
	Text(PrologueHint1)
	CAM1 [1,9]
	FlashCursor(1,9,60)
	NoFade
	ENDA

GalleyReinforcements:
	CHECK_EVENTID 0x9
	BEQ 1 0x0 0xC
		SpawnReinforcements(GalleyUnits)
		GOTO 2
		LABEL 1
	ENUF 0xA
	LABEL 2
	NoFade
	ENDA

Reinforcements:
	SpawnReinforcements(ExtraUnits)
	SpawnReinforcements(ExtraUnits2)
	NoFade
	ENDA

BrigandsAppear:
	SpawnReinforcements(StairUnits)
	NoFade
	ENDA

CharacterBasedEvents:
	
	END_MAIN

MiscBasedEvents:
	AREA 0x9 SetUpGalley [14,3] [20,10]
	CauseGameOverIfLordDies
	DefeatBoss(EndingScene)
	END_MAIN

SetUpGalley:
	CHECK_ALLEGIANCE 0xFFFF
	BNE 1 0x0 0xC
		GOTO 2
		LABEL 1
	ENUF 0x9
	LABEL 2
	NoFade
	ENDA

LocationBasedEvents:
	Village(0xB,LeftVillage,1,9)
	Village(0xC,MidVillage,6,11)
	Village(0xD,RightVillage,10,1)
	END_MAIN

LeftVillage:
	MUSS SkyEventSong
	Text(HouseBG,PrologueOskarBlancHouse)
	SpawnReinforcements(VillageUnits)
	FlashCursor(Oskar,60)
	Text(PrologueOskarBlancAfterRecruit)
	MURE 2
	NoFade
	ENDA

MidVillage:
	VillageEventItem(PrologueHouse2,HouseBG,Thunder)

RightVillage:
	VillageEventItem(PrologueHouse1,HouseBG,RedGem)

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
	UNIT Sanders Mercenary 0x0 Level(1,Ally,0) [3,0] 0 0 1 REDA3R3 [FalconBlade,Vulnerary] NoAI
	UNIT Finn LanceKnight 0x0 Level(3,Ally,0) [0,0] 0 0 1 REDA2R2 [IronLance,Javelin,Vulnerary] NoAI
	UNIT Scott Cavalier 0x0 Level(2,Ally,0) [0,0] 0 0 1 REDA4R2 [IronSword,IronLance] NoAI
	UNIT Renge Cleric 0x0 Level(1,Ally,0) [0,0] 0 0 1 REDA3R1 [Heal,Vulnerary] NoAI
	UNIT Marisa MageF 0x0 Level(2,Ally,0) [0,0] 0 0 1 REDA3R2 [Fire,Vulnerary] NoAI
	UNIT

VillageUnits:
	UNIT Oskar Fighter 0x0 Level(3,Ally,0) [1,9] 0 0 1 REDA1R10 [SteelAxe,Vulnerary] NoAI
	UNIT Blanc FighterF 0x0 Level(2,Ally,0) [1,9] 0 0 1 REDA2R10 [IronAxe,Hatchet,Vulnerary] NoAI
	UNIT

EnemyUnits:

	UNIT Batta Brigand 0x0 Level(4,Enemy,0) [19,6] 0 0 0 0 [SteelAxe,HandAxe] AttackInRangeAI

	UNIT BattaBeast Brigand 0x0 Level(1,Enemy,1) [18,10] 0 0 0 0 [IronAxe] BrigandAI

	UNIT BattaBeast Mercenary 0x0 Level(1,Enemy,1) [3,10] 0 0 0 0 [IronSword] AttackInRangeAI
	UNIT BattaBeast Mercenary 0x0 Level(2,Enemy,1) [9,7] 0 0 0 0 [IronSword] AttackInRangeAI
	UNIT BattaBeast Mercenary 0x0 Level(3,Enemy,1) [8,0] 0 0 0 0 [IronSword] AttackInRangeAI

	UNIT BattaBeast Soldier 0x0 Level(1,Enemy,1) [10,6] 0 0 0 0 [SlimLance] AttackInRangeAI
	UNIT BattaBeast Soldier 0x0 Level(2,Enemy,1) [13,3] 0 0 0 0 [IronLance] AttackInRangeAI
	UNIT BattaBeast Soldier 0x0 Level(3,Enemy,1) [18,6] 0 0 0 0 [IronLance] AttackInRangeAI
	UNIT BattaBeast Soldier 0x0 Level(3,Enemy,1) [10,12] 0 0 0 0 [SteelLance] AttackInRangeAI

	UNIT BattaBeast Fighter 0x0 Level(1,Enemy,1) [1,5] 0 0 0 0 [IronAxe] DefaultAI
	UNIT BattaBeast Fighter 0x0 Level(1,Enemy,1) [2,6] 0 0 0 0 [IronAxe] DefaultAI
	UNIT BattaBeast Fighter 0x0 Level(1,Enemy,1) [17,9] 0 0 0 0 [IronAxe] AttackInRangeAI
	UNIT BattaBeast Fighter 0x0 Level(2,Enemy,1) [4,12] 0 0 0 0 [IronAxe] AttackInRangeAI

	UNIT BattaBeast Archer 0x0 Level(1,Enemy,1) [2,10] 0 0 0 0 [IronBow] AttackInRangeAI
	UNIT BattaBeast Archer 0x0 Level(1,Enemy,1) [17,1] 0 0 0 0 [IronBow] AttackInRangeAI

	UNIT BattaBeast Mage 0x0 Level(2,Enemy,1) [11,12] 0 0 0 0 [Fire] AttackInRangeAI

	UNIT

StairUnits:
	UNIT BattaBeast Brigand 0x0 Level(1,Enemy,1) [17,0] 0 0 0 0 [IronAxe] BrigandAI
	UNIT BattaBeast Brigand 0x0 Level(1,Enemy,1) [18,0] 0 0 0 0 [IronAxe] BrigandAI
	UNIT BattaBeast Brigand 0x0 Level(1,Enemy,1) [20,10] 0 0 0 0 [IronAxe] BrigandAI
	UNIT

ExtraUnits:
	UNIT BattaBeast Cavalier 0x0 Level(3,Enemy,1) [0,0] 0 0 1 REDA0R0 [IronLance] DefaultAI
	UNIT BattaBeast FreeKnight 0x0 Level(3,Enemy,1) [0,0] 0 0 1 REDA0R0 [IronSword] DefaultAI
	UNIT

ExtraUnits2:
	UNIT BattaBeast Fighter 0x0 Level(2,Enemy,1) [0,12] 0 0 1 REDA0R12 [IronAxe] DefaultAI
	UNIT BattaBeast Mercenary 0x0 Level(3,Enemy,1) [0,12] 0 0 1 REDA0R12 [SlimSword] DefaultAI
	UNIT

GalleyUnits:
	UNIT BattaBeast Soldier 0x0 Level(3,Enemy,1) [17,0] 0 0 0 0 [IronLance] DefaultAI
	UNIT BattaBeast Soldier 0x0 Level(3,Enemy,1) [18,0] 0 0 0 0 [IronLance] DefaultAI
	UNIT BattaBeast Fighter 0x0 Level(1,Enemy,1) [20,10] 0 0 0 0 [IronAxe] DefaultAI
	UNIT

Traps1:
	ENDTRAP
	ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
	#ifdef _DEBUG_ROOM_
		MNC2 0x4E //debug room
	#endif
	#ifndef _DEBUG_ROOM_
	MUSC ASunnyPathSong
	SetBackground(Castle_Garden_Past)
	Text(PrologueOP)
	FADI 16
	MUSC 0x4C // birds
	SetBackground(Salehs_House)
	TEXTSTART
		TEXTSHOW PrologueOP2
		TEXTEND
		MUSC InvestigationSong
		TEXTCONT
		TEXTEND
	FADI 16
	CLEAN
	LOAD1 0x1 EnemyUnits
	ENUN
	FADU 16
	LOAD1 0x1 Units1
	ENUN
	FlashCursor(Sanders,60)
	Text(PrologueOP3)
	MUSC ToccataAndDoomSong
	CAM1 Batta
	FlashCursor(Batta,60)
	Text(PrologueOP4)
	NoFade
	#endif
	ENDA

EndingScene:
	MUSC WereRangersSong
	SetBackground(Outside_Village_Evening)
	Text(PrologueEnding)
	MNC2 0x1
	NoFade
	ENDA